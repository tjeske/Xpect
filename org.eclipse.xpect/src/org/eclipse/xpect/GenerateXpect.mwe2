module org.eclipse.xpect.GenerateXpect

import org.eclipse.xtext.xtext.generator.*
import org.eclipse.xtext.xtext.generator.model.project.*

var projectName = "org.eclipse.xpect"
var rootPath = ".."
var runtimeProject = "${rootPath}/${projectName}"
var genmodelURI = "platform:/resource/${projectName}/model/Xpect.genmodel"

Workflow {
	
	component = XtextGenerator {
		configuration = {
			project = StandardProjectConfig {
				baseName = "org.eclipse.xpect"
				rootPath = rootPath
				runtimeTest = {
					enabled = true
				}
				eclipsePlugin = {
					enabled = true
				}
				eclipsePluginTest = {
					enabled = false
				}
				createEclipseMetaData = true
			}
			code = {
				encoding = "UTF-8"
				lineDelimiter = "\n"
				fileHeader = "/*\n * generated by Xtext \${version}\n */"
				preferXtendStubs = false
			}
		}
		language = StandardLanguage {
			referencedResource = "${genmodelURI}"
			name = "org.eclipse.xpect.Xpect"
			fileExtensions = "xpect"

			serializer = {
				generateStub = false
			}
			validator = {
			}
			generator = {
				generateStub = false
				generateXtendStub = true
			}
			junitSupport = {
				generateStub = false
			}
			
			parserGenerator = {
				combinedGrammar = false
				options = {
					
				}
			}
			
			fragment = org.eclipse.xtext.generator.adapter.FragmentAdapter {
			fragment = de.itemis.statefullexer.StatefulLexerFragment {
				runtime = true
				lexerBaseClass = "org.eclipse.xpect.services.Lexer"
				antlrParam = "-lib"
				antlrParam = "${runtimeProject}/src-gen/org/eclipse/xpect/parser/antlr/lexer"
			}
			
			}
			fragment = org.eclipse.xtext.generator.adapter.FragmentAdapter {
			fragment = de.itemis.statefullexer.StatefulLexerFragment {
				lexerBaseClass = "org.eclipse.xpect.services.Lexer"
				highlighting = true
				antlrParam = "-lib"
				antlrParam = "${runtimeProject}.ide/src-gen/org/eclipse/xpect/ide/contentassist/antlr/lexer"
			}
			
			}
			fragment = org.eclipse.xtext.generator.adapter.FragmentAdapter {
			fragment = de.itemis.statefullexer.StatefulLexerFragment {
				lexerBaseClass = "org.eclipse.xpect.ide.services.Lexer"
				contentAssist = true
				antlrParam = "-lib"
				antlrParam = "${runtimeProject}.ide/src-gen/org/eclipse/xpect/ide/contentassist/antlr/lexer"
			}
			
			}
		}
	}
	
	component = org.eclipse.emf.mwe2.ecore.EcoreGenerator {
		genModel = genmodelURI
		srcPath = "${runtimeProject}/src"
	}
}
